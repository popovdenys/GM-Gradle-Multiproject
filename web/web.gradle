/**
 * Web project
 */

apply plugin: 'war'

repositories {
    mavenCentral()

    ivy {
        name 'jQuery'
        url 'http://code.jquery.com'
        layout 'pattern', {
            artifact '[module]-[revision](.[classifier]).[ext]'
        }
    }
}

configurations {
    jquery
}

dependencies {
    compile project(':repository')

    compile 'org.apache.tomcat:tomcat-websocket:9.0.5'

    compile 'javax.servlet:jstl:1.2'

    compile 'org.json:json:20180130'

    // Use servlet api
    compile 'javax:javaee-web-api:8.0'

    jquery group: 'jquery', name: 'jquery', version: '3.3.1', classifier: '', ext: 'js'
}


/**
 * Get external libs
 */
task getExternalJQuery(type: Copy) {
    from configurations.jquery
    into "$webAppDir/interface/js/libs"
}

task getExternalReact(type: Copy) {
    from 'build/react'
    into "$webAppDir/interface/js/app/react"
}

task copyToTomcat(type: Copy, group: 'deploy', description: 'deploy to Apache Tomcat') {
    dependsOn getExternalJQuery
    from war.outputs.files
    into "${System.properties['user.home']}/apache-tomcat/v9.0.5/webapps"
    rename { String fileName ->
        'galaxy.war'
    }
    doLast {
        println 'deploy to Apache Tomcat'
    }
}
